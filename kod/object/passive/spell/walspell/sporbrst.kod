% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SporeBurst is WallSpell

constants:

   include blakston.khd

resources:

   include sporbrst.lkod

   SporeBurst_name_rsc = "spore burst"
   SporeBurst_icon_rsc = isporecl.bgf
   SporeBurst_desc_rsc = \
      "Summons forth noxious clouds that paralyze any within it's bounds.  "
      "Requires potent reagents specific to both Qor and Faren."
      
   SporeBurst_caster = "A noxious cloud appears around %s%q."
   SporeBurst_target = "A noxious cloud surrounds you!"
   SporeBurst_failed_rsc = \
      "There is too much summoning magic focused here to cause a spore burst."
   SporeBurst_sound = farenspore822m.wav


classvars:

   vrName = SporeBurst_name_rsc
   vrIcon = SporeBurst_icon_rsc
   vrDesc = SporeBurst_desc_rsc

   viSpell_num = SID_SPORE_BURST
   viSchool = SS_FAREN
   viSpell_level = 6
   viMana = 20
   viSpellExertion = 5
   viCast_time = 3000
   viMeditate_ratio = 75

   viChance_To_Increase = 15

   vrSucceed_wav = SporeBurst_sound

   viHarmful = TRUE

   % Sporeburst can't kill anyone, so don't warn about being a murderer.
   viWallCanKill = FALSE

   % We technically have more elements, but that would never let us cast this
   %  while another wall spell is active.  A little cheat here, only count 9
   %  elements.
   viNumElements = 9

   vrSummonLimitMsg = SporeBurst_failed_rsc

   vbTargetSpore = TRUE

properties:


messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&PurpleMushroom,2],plReagents);
      plReagents = Cons([&ShamanBlood,1],plReagents);

      return;
   }

   CastSpell(who=$,lTargets=$,iSpellPower=0)
   {
      local oTarget;

      if lTargets = $
      {
         Debug("Spore burst cast will nil target list");

         return;
      }

      oTarget = First(lTargets);

      if oTarget = $
      {
         Debug("Spore burst cast will nil target");

         return;
      }

      Send(oTarget,@MsgSendUser,#message_rsc=SporeBurst_target,
            #parm1=Send(oTarget,@GetCapDef),#parm2=Send(oTarget,@GetName));

      Send(who,@MsgSendUser,#message_rsc=SporeBurst_caster);

      propagate;
   }

   GetNumSpellTargets()
   {
      return vbTargetSpore;
   }

   PlaceWallElements(who=$,lTargets=$,iSpellPower=0)
   {
      local i, oRoom, iCounter, iDiff, oActiveFog, oPassiveFog, iRadius,
            iDuration, iRow, iCol, oTarget, iAngle, iFine_Row, iFine_Col,
            iXStep, iYStep, iHalfrow, iHalfCol, iHalfFine_Row,
            iHalfFine_Col;

      if (lTargets = $)
      {
         return;
      }

      oTarget = First(lTargets);

      iAngle = Send(oTarget,@GetAngle);
      iDuration = Send(self,@GetDuration,#iSpellpower=iSpellpower);

      oRoom = Send(oTarget,@GetOwner);

      iRow = Send(oTarget,@GetRow);
      iCol = Send(oTarget,@GetCol);

      oActiveFog = Create(&ActiveSporeCloud,#oRoom=oRoom,#Caster=who,#duration=iDuration);
      Send(oRoom,@NewHold,#what=oActiveFog,#new_row=iRow,#new_col=iCol,
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      % Create fog in eight cardinal directions
      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow+1),#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow+1),#new_col=iCol,
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow+1),#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow-1),#new_col=(iCol+1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow-1),#new_col=iCol,
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=(iRow-1),#new_col=(iCol-1),
           #fine_row=FINENESS_HALF,#fine_col=FINENESS_HALF);


      % Create four "filler" fog in corners.
      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=iCol,
           #fine_row=0,#fine_col=0);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=iCol,
           #fine_row=0,#fine_col=FINENESS);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=iCol,
           #fine_row=FINENESS,#fine_col=0);

      oPassiveFog = Create(&SporeCloud,#oRoom=oRoom,#oRoom=oRoom,#Caster=who,#Active_Elem=oActiveFog,
                        #Duration=iDuration);
      Send(oRoom,@NewHold,#what=oPassiveFog,#new_row=iRow,#new_col=iCol,
           #fine_row=FINENESS,#fine_col=FINENESS);

      propagate;
   }

   GetDuration(iSpellPower=$, caster=0)
   {
      local iDuration;

      % Duration, in seconds
      iDuration = iSpellPower * 2; 
      iDuration = bound(iDuration,30,200);
      % Convert to ms.
      iDuration = iDuration * 1000;

      return iDuration;
   }

   ModifyMonsterBehavior(mob = $)
   {
      Send(mob,@SetBehaviorFlag,#flag=AI_NOMOVE,#value=TRUE);
      Send(mob,@SetBehaviorFlag,#flag=AI_NOFIGHT,#value=TRUE);

      return;      
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

